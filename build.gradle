plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.2.2'
}

shadowJar {
    relocate 'org.apache.commons.net', 'your.plugin.shaded.org.apache.commons.net'
    archiveClassifier.set("") // Remove the "-all" suffix
}

group = 'tr.alperendemir'
version = '1.2'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:1.21.10-R0.1-SNAPSHOT")
    implementation("commons-net:commons-net:3.9.0")
    implementation('com.dropbox.core:dropbox-core-sdk:7.0.0') {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'com.fasterxml.jackson', module: 'jackson-bom'
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
    filesMatching('config.yml') {
        expand props
    }
}
